// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU. Computes one of the following functions:
 * x+y, x-y, y–x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs.
 * Which function to compute is determined by 6 input bits 
 * denoted zx, nx, zy, ny, f, no.
 * The computed function's value is called "out".
 * In addition to computing out, the ALU computes two 
 * 1-bit outputs called zr and ng:
 * if out == 0, zr = 1; otherwise zr = 0;
 * If out < 0, ng = 1; otherwise ng = 0.
 * The 6-bit combinations (zx,nx,zy,ny,f,no) and 
 * their effect are documented in the book. 
 */

// Implementation: the ALU manipulates the x and y
// inputs and then operates on the resulting values, 
// as follows:
// if (zx  == 1) sets x = 0        // 16-bit constant
// if (nx  == 1) sets x = ~x       // bitwise "not"
// if (zy  == 1) sets y = 0        // 16-bit constant
// if (ny  == 1) sets y = ~y       // bitwise "not"
// if (f   == 1) sets out = x + y  // integer 2's-complement addition
// if (f   == 0) sets out = x & y  // bitwise And
// if (no  == 1) sets out = ~out   // bitwise Not
// if (out == 0) sets zr = 1
// if (out < 0)  sets ng = 1

  
CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute  out = x + y (if f == 1) or out = x & y (if == 0)
        no; // negate the out outp

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
	
	//Zero x when zx is true
    Mux16(a=x,b=false,sel=zx,out=x1);
	//Negate x when nx is true
	Not16(in=x1,out=x2);
	Mux16(a=x1,b=x2,sel=nx,out=realx);
	
	//Zero y when zy is true
	Mux16(a=y,b=false,sel=zy,out=y1);
	//Negate y when ny is true
	Not16(in=y1,out=y2);
	Mux16(a=y1,b=y2,sel=ny,out=realy);
	
	//Compute the output
	And16(a=realx,b=realy,out=andtotal);
	Add16(a=realx,b=realy,out=sumtotal);
	Mux16(a=andtotal,b=sumtotal,sel=f,out=positiveoutput);
	
	//Deal with no flag
	Not16(in=positiveoutput,out=negoutput);
	Mux16(a=positiveoutput,b=negoutput,sel=no,out=out,out=finaloutput);
	
	//Deal with ng flag
	And16(a=true,b=finaloutput,out[15]=ng);

	//Deal with zr flag
	And16(a=true,b=finaloutput,out[0..7]=out81,out[8..15]=out82);
	Or8Way(in=out81,out=zr0);
	Or8Way(in=out82,out=zr1);
	Or(a=zr0,b=zr1,out=zr2);
	Not(in=zr2,out=zr);
}
